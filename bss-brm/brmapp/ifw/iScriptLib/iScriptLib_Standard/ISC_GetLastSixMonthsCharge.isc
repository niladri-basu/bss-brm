//==============================================================================
//
// Copyright (c) 2006 Oracle. All rights reserved.
// 
// This material is the confidential property of Oracle Corporation
// or its licensors and may be used, reproduced, stored or transmitted
// only in accordance with a valid Oracle license or sublicense agreement.
//
//------------------------------------------------------------------------------
//-------------------------------------------------------------------------------
// Block: ISC_GetLastSixMonthsCharge.isc
//-------------------------------------------------------------------------------
// Module Description:
//
// Contains functions generally required to be used by the  expressions in discounting
// Can be used in other iScripts too.
//


use EXT_FList;
use EXT_Opcode;

//Global Variables

Bool DEBUG = false;

function Decimal getLastSixMonthsCharge
{
  logPipeline("getLastSixMonthsCharge() " +"\n");

  String s1 = "";
  String s2 = "";
  fListCreateNew();

  //Add the poid, flags and template to the flist
  fListSetPoid( "PIN_FLD_POID", "0.0.0.1 /search -1 0" );
  fListSetLong( "PIN_FLD_FLAGS", 256 );
  fListSetString( "PIN_FLD_TEMPLATE", "Select X from /item where F1 = V1 AND F2 >= V2 " );

  //Arg1 is the service object.
  String svcID = edrString(DETAIL.CUST_A.DL.DISCOUNT_OWNER_ID, 0, 0);
  String svcType = edrString(DETAIL.CUST_A.DL.DISCOUNT_OWNER_TYPE,0, 0);
  if(DEBUG == true){
     logPipeline("Service Type:\n" +svcType +" "+svcID +"\n");
  }
  //The service Id is in the format 1_12345. Need to seperate the
  //database and the poid id.
  String ListArray[];
  Long nbElem = strSplit( ListArray, svcID, "_" );

  String svcObjStr = "0.0.0."+ListArray[0]+" "+svcType+" "+ListArray[1]+" 0";
  fListPushElem( "PIN_FLD_ARGS", 1 );
  fListSetPoid( "PIN_FLD_SERVICE_OBJ", svcObjStr);
  fListPopElem();

  //Arg2 is the effective_t. This is current date - 6 Months
  Date now = sysdate();
  Date past = dateAdd(now,0,-6,0,0);
  fListPushElem( "PIN_FLD_ARGS", 2 );
  fListSetDate( "PIN_FLD_EFFECTIVE_T", past );
  fListPopElem();

  //set the results array
  Decimal iTotal = 0.0;
  fListPushElem ( "PIN_FLD_RESULTS",  0  );
  fListSetDecimal( "PIN_FLD_ITEM_TOTAL", iTotal);
  fListPopElem ();

  if(DEBUG == true) {
     String fStr = fListToString();
     logPipeline("Printing the INPUT FLIST:\n");
     logPipeline(fStr);
  }

  //Execute the opcode PCM_OP_SEARCH
  if (opcodeExecuteInternal(7, 0) == false )
  {
    // Opcode failed
    fListGetErrorText( s1, s2 );
    logPipeline("PCM_OP_SEARCH failed: " + s1 + " " + s2 + "\n");
    return INVALID_DECIMAL;
  }

  if(DEBUG == true) {
     fListGetErrorText( s1, s2 );
     logPipeline("PCM_OP_SEARCH done: " + s1 + " " + s2 + "\n");

     String fRStr = fListToString();
     logPipeline("Printing the RETURN FLIST:\n");
     logPipeline(fRStr);
  }

  //Loop through the results and aggregate the item total
  Long resultCount = fListNumElem ( "PIN_FLD_RESULTS" );
  Long i;
  Decimal tempTotal = 0.0;
  Decimal itemTotal = 0.0;

  for ( i = 0; i < resultCount; i = i + 1 )
  {
    tempTotal = fListDecimal ("PIN_FLD_RESULTS", i, "PIN_FLD_ITEM_TOTAL");
    itemTotal = itemTotal + tempTotal;

  }

  if(DEBUG == true) {
     logPipeline("getLastSixMonthsCharge() return:\n" +decimalToStr(itemTotal) +"\n");
  }
  return itemTotal;
}
